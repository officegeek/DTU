---
layout: default
title: Online 24-03-2022
nav_order: 3
parent: VBA
published: true
---
[HOME](../README.md)
# Online - 24-03-2022

# Emner
- [Excel til 24-03-2022](#excel-fil)
- [DataImport.dotx](#word-fil)
- [Variable](#variable)
- [Word](#word)

# Excel fil
Jeg har oprette en Excel som indeholder en Tabel med data som jeg brugere - Den Excel fil kan du hente her [online_24032022.xlsm](./filer/online_24032022.xlsm). Den indeholder **ikke** noget VBA kode.

Min Excel fil, der indeholder den **færdige VBA** kode, kan hentes her [online_24032022_slut.xlsm](./filer/online_24032022_slut.xlsm)

Jeg vil dog anbefale at i selv skriver VBA koden og får det til at virke - **Det vil sige brug kun min fil hvis i kører helt fast :-)**

# Word fil
Her er den Word fil jeg brugere til VBA eksporten.

[DataImport.dotx](./filer/DataImport.dotx)

Det er en Word skabelon (*.dotx*), så det giver mening at gemme den som et almindeligt Word dokument (*.docx*)

# Variable
En variabel brugere du til at gennem værdier mens dit program kører.

Forskellen mellem konstanter og variabler er at konstanter har en **fast** værdi, mens variabler kan **variere** i værdien.

## Erklær en variabel eller konstant
Det er god kode stil at erklære en variabel eller konstant inden du bruger den. 

Det er godt nok frivilligt om du gør det i VBA.

At erklære den, vil sige at du definerer hvilken datatype en variabel eller konstant er.

Du erklærer en variabel med koden:

    DIM *navn* AS *datatype*

## Datatyper
En variabel eller konstant er defineret med forskellige **datatyper**.

- **byte** er en positiv talværdi mellem 0 og 255. Den fylder ca. 1 byte i hukommelsen.
- **boolean** er en værdi der enten kan være sand (true) eller falsk (false). Den fylder ca. 2 bytes.
- **integer** er en heltalsværdi mellem -32768 og +32768. Den fylder ca. 2 bytes.
 -**long** er også en heltalsværdi, men kan være mellem -2.147.483.648 og +2.147.483.648. Den fylder ca. 4 bytes.
- **single** er et decimaltal mellem -3,4e38 og +3,4e38 og fylder ca. 4 bytes.
- **double** er et decimaltal mellem -1,8e308 og +1,8e308 og fylder ca. 8 bytes.
 -**currency** er et decimaltal mellem -922.337.203.685.477,5808 og
+922.337.203.685.477,5808 og fylder ca. 8 bytes.
- **date** er en dato der kan være fra den 1. januar år 100 til den 31. december 9999. Den fylder ca. 8 bytes.
- **string** er en tekststreng der kan have op til 65.500 tegn, hvis den defineres som en konstant. Hvis den defineres som variabel, kan den have ca. 2 milliarder tegn.

## Video
<div style="position: relative; padding-bottom: 106.10526315789474%; height: 0;"><iframe src="https://www.loom.com/embed/148b5d147b7a445ca275d704fc5d012d" frameborder="0" webkitallowfullscreen mozallowfullscreen allowfullscreen style="position: absolute; top: 0; left: 0; width: 100%; height: 100%;"></iframe></div>


# Word
Vi har en Tabel i Excel der skal overføres til Word, da det er noget vi skal gøre hver gang data i Excel bliver opdateret vil vi gerne gøre det via en makro.

![](./image/VBA_Word_1.jpg)

Denne tabel skal over i et Word dokument, **DataImport.dotx**, dette Word dokument indeholder lidt tekst og 2 "*Bogmærker*" (*Bookmark*)

- bmkDato
- bmkDataTabel

![](./image/VBA_Word_2.jpg)

Jeg forudsætter at Excel filen og Word dokumentet er i den **sammen mappe**.

## Video
**Video 1** - Variabler - Ref til Word
<div style="position: relative; padding-bottom: 108.35073068893529%; height: 0;"><iframe src="https://www.loom.com/embed/3dd1185b512d4aec91c10c04fbc03376" frameborder="0" webkitallowfullscreen mozallowfullscreen allowfullscreen style="position: absolute; top: 0; left: 0; width: 100%; height: 100%;"></iframe></div>

**Video 2** - Start af Word
<div style="position: relative; padding-bottom: 106.10526315789474%; height: 0;"><iframe src="https://www.loom.com/embed/be82012b8cbf4aae9949f12a539f1d2e" frameborder="0" webkitallowfullscreen mozallowfullscreen allowfullscreen style="position: absolute; top: 0; left: 0; width: 100%; height: 100%;"></iframe></div>

**Video 3** - Indsæt dags dato i Word
<div style="position: relative; padding-bottom: 106.10526315789474%; height: 0;"><iframe src="https://www.loom.com/embed/e4533dc712b6445abd2196cc9f729281" frameborder="0" webkitallowfullscreen mozallowfullscreen allowfullscreen style="position: absolute; top: 0; left: 0; width: 100%; height: 100%;"></iframe></div>

**Video 4** - Indsæt tabellen i Word
<div style="position: relative; padding-bottom: 106.10526315789474%; height: 0;"><iframe src="https://www.loom.com/embed/f214a07208164fb69447a4b6df2ff748" frameborder="0" webkitallowfullscreen mozallowfullscreen allowfullscreen style="position: absolute; top: 0; left: 0; width: 100%; height: 100%;"></iframe></div>

**Video 5** - Luk Word objektet
<div style="position: relative; padding-bottom: 106.10526315789474%; height: 0;"><iframe src="https://www.loom.com/embed/755b778be973490192580d0f150412a5" frameborder="0" webkitallowfullscreen mozallowfullscreen allowfullscreen style="position: absolute; top: 0; left: 0; width: 100%; height: 100%;"></iframe></div>


## VBA koden

```vb
Sub Export_Word()
    ' Variabler
    ' Navn på Word dokument
    Const strWordDokument As String = "DataImport.dotx"
    
    ' Word object
    Dim wdApp As Word.Application
    Dim wdDoc As Word.Document
    Dim wdBmkRange As Word.Range
    
    ' Excel object
    Dim wbBook As Workbook
    Dim wbSheet As Worksheet
    Dim ranRapport As Range
    
    Set wbBook = ThisWorkbook ' Den aktive fil
    Set wbSheet = wbBook.Worksheets("Tabel_OmsAfd") ' ref til ark
    Set ranRapport = wbSheet.Range("OmsAfd") ' Ref. til tabellen - Tabel_OmsAfd
    
    ' Start Word
    Set wdApp = New Word.Application ' Word app
    Set wdDoc = wdApp.Documents.Open(wbBook.Path & "\" & strWordDokument) ' Åben Word doc
    
    ' Indsæt dato
    Set wdBmkRange = wdDoc.Bookmarks("bmkDato").Range ' Bogmærke: bmkDato
    wdBmkRange.InsertAfter Date
    
    ' Indsæt tabellen
    Set wdBmkRange = wdDoc.Bookmarks("bmkDataTabel").Range ' Bogmærke: bmkDataTabel
    
    ' Kopi af tabellen: OmsAfd
    ranRapport.Copy
    
    With wdBmkRange
        .Select ' Vælg boogmærket
        .PasteSpecial Link:=False, _
            DataType:=wdPasteMetafilePicture, _
            Placement:=wdInLine, _
            DisplayAsIcon:=False ' Indsæt Tabellen som et billede, ingen link
    End With
    
    ' Luk
    With wdDoc
        .SaveAs2 wbBook.Path & "\Word_Rapport_" & Format(Date, "dd_mm_yyyy") & ".docx", _
            FileFormat:=wdFormatDocumentDefault
        .Close ' Luk Dokument
    End With

    wdApp.Quit ' Luk word
    
End Sub
```
